ui_print("");
ui_print("Welcome to");
ui_print("InstigatorX Kernel");
ui_print("For CM10.1");
ui_print("");
ui_print("Install script");
ui_print("Courtesy of h0tw1r3");
ui_print("");

show_progress(1.300000, 7);

ifelse( is_mounted("/system") == "", (
    run_program("/sbin/mount", "/system");
    ifelse( is_mounted("/system") == "", (
        ui_print("?? Failed to mount /system");
        abort();
    ));
));

ui_print("Setting up tools...");

assert(
    package_extract_file("unpackbootimg", "/tmp/unpackbootimg"),
    package_extract_file("mkbootimg", "/tmp/mkbootimg"),
    package_extract_file("zImage", "/tmp/zImage")
);

set_perm(0, 0, 0755, "/tmp/unpackbootimg");
set_perm(0, 0, 0755, "/tmp/mkbootimg");

ui_print("Extracting boot partition...");

assert(
    run_program("/sbin/sleep", "1"),
    run_program("/sbin/dd",
        "if=/dev/block/mmcblk0p7",
        "of=/tmp/boot.img"
    ),
    run_program("/sbin/sleep", "1"),
    run_program("/tmp/unpackbootimg",
        "--output","/tmp",
        "--input","/tmp/boot.img"
    ),
    run_program("/sbin/sleep", "1")
);

ui_print("Rebuilding boot image...");

assert(
    run_program("/tmp/mkbootimg",
        "--kernel","/tmp/zImage",
        "--cmdline","androidboot.hardware=qcom user_debug=31 zcache",
        "--base","0x80200000",
        "--pagesize","2048",
        "--ramdiskaddr","0x81500000",
        "--ramdisk","/tmp/boot.img-ramdisk.gz",
        "-o","/tmp/new_boot.img"
    ),
    run_program("/sbin/sleep", "1")
);

ui_print("Flashing new boot image...");

assert(
    run_program("/system/bin/ls","/tmp/new_boot.img"),
    write_raw_image("/tmp/new_boot.img", "/dev/block/mmcblk0p7"),
    run_program("/sbin/sleep", "1")
);

ui_print("Updating kernel modules...");

assert(
    delete_recursive("/system/lib/modules"),
    package_extract_dir("realsystem", "/system")
);

ui_print("Success!");
